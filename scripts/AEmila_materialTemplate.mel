global proc AEmila_materialSurfaceNew ( string $nodeName )
{
	print "AEmila_materialSurfaceNew\n";
	$nodeName = `basenameEx $nodeName`;

	string $parent = `setParent -q`;

	python("mila_material_ui.mila_tree( \"" + $nodeName + "\",\"" + $parent  + "\" )");

	setParent $parent;
}

global proc AEmilaComponentNew ( string $nodeName )
{
	columnLayout -adj true "AEmila_component_layout";
	setParent "..";
}

global proc AEmila_materialSurfaceReplace ( string $nodeName )
{
	print "AEmila_materialSurfaceReplace\n";
	string $parent = `setParent -q`;
	
	print "AEmila_materialSurfaceReplace\n";
	$nodeName = `basenameEx $nodeName`;
	
	python("mila_material_ui.mila_tree_update_AE( \"" + $nodeName + "\",\"" + $parent  + "\" )");
}

global proc AEmilaBumpNew(string $attr) {
	attrNavigationControlGrp
		-label "Overall Bump"
		-annotation ""
		-attribute $attr
		-columnWidth 2 30
		-adjustableColumn 2
		bumpControlMila;
}

global proc AEmilaBumpReplace(string $attr) {
	attrNavigationControlGrp -edit
		-attribute $attr
		bumpControlMila;
	scriptJob -replacePrevious
		-parent bumpControlMila
		-connectionChange $attr
		("AEcheckMilaBumpConnection " + $attr);
}

global proc AEcheckMilaBumpConnection(string $attr) {
	// on disconnect, random values populate the attr, but it needs
	// to be set to 0,0,0 if no bump node is connected
	if (!`connectionInfo -id $attr`) {
		setAttr $attr 0 0 0;
	}
}
//*******

global proc doNothing(string $useless)
{
}

global proc AEmila_materialTemplate(string $nodeName)
{

	python("import mila_material_ui");

	AECommonStrings();						// Define common strings

	AEswatchDisplay $nodeName;

    editorTemplate -callCustom "AEmila_materialSurfaceNew" "AEmila_materialSurfaceReplace" "shader";

	editorTemplate -beginScrollLayout;

	    editorTemplate -callCustom "AEmilaComponentNew" "doNothing" "";

		editorTemplate -collapse 1 -beginLayout "Base Properties";
	                //editorTemplate -label "Backface Layer/Mix" -addControl "backface_shader";
	                editorTemplate -label "Visibility" -addControl "visibility";
	                editorTemplate -label "Thin-walled" -addControl "thin_walled";
	                editorTemplate -label "Show Framebuffer" -addControl "show_framebuffer";

	                editorTemplate -callCustom "AEmilaBumpNew" "AEmilaBumpReplace" "bump";
		editorTemplate -endLayout;


   	AEmentalrayBaseTemplate $nodeName;

	editorTemplate -endScrollLayout;
}

